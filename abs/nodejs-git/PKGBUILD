# Contributor: Dongsheng Cai <dongsheng@moodle.com>
pkgname=nodejs-git
pkgver=20101219
pkgrel=1
pkgdesc="Evented I/O for V8 javascript"
arch=('i686' 'x86_64')
url="http://nodejs.org/"
license=('custom:nodejs')
depends=('gcc' 'coreutils' 'python2')
_gitroot='git://github.com/ry/node.git'
_gitname='node'

build() {
    cd ${srcdir}
    if [ -d $_gitname ]; then
        cd $_gitname
        msg "Updating local files"
        git checkout -- .
        git pull . master
    else
        msg "Cloning git repository"
        git clone $_gitroot
    fi
    cd ${srcdir}/${_gitname}
    # python2 fix
    for file in $(find . -name '*.py' -print) wscript tools/waf-light tools/node-waf; do
        sed -i 's_^#!.*/usr/bin/python_#!/usr/bin/python2_' "$file"
        sed -i 's_^#!.*/usr/bin/env.*python_#!/usr/bin/env python2_' "$file"
    done
    sed -i "s|cmd_R = 'python |cmd_R = 'python2 |" wscript
    sed -i "s|python |python2 |" Makefile

    ./configure --prefix=/usr || return 1
    make || return 1
}

package() {
    cd ${srcdir}/${_gitname}

    make DESTDIR="$pkgdir/" install
    install -D -m644 LICENSE $pkgdir/usr/share/licenses/${pkgname}/LICENSE
}


